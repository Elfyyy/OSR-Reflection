
type
  TTile3D = record
    X, Y, Z: Extended;
  end;

  TRotation = TTile3D;

  TMatrix = record
    Data: Array of Extended;
  end;

  TScene = record
    TilePosition, PlayerTile, CameraPosition: TTile3D;
    TileRotation, CameraRotation: TRotation;
  end;

var
  CURVESIN, CURVECOS: TIntegerArray;

procedure TReflectionTiles.SetupCurves;
var
  I: Integer;
begin
  SetLength(CURVESIN, 2048);
  SetLength(CURVECOS, 2048);
  for I := 0 to 2047 do begin
    CURVESIN[I] := Round(65536.0 * Sin(I * 0.0030679614999999999));
    CURVECOS[I] := Round(65536.0 * Cos(I * 0.0030679614999999999));
  end;
end;

function TReflectionTiles.GetGlobalTile: TTile;
var
  Me: TReflectPointer;
begin
  Me.GetFieldObject(0, Client_LocalPlayer);
  Result.X := Reflect.Smart.GetFieldInt(0, Client_BaseX) +
    Reflect.Smart.GetFieldInt(Me.Reference, Actor_WorldX) div 128;
  Result.Y:= Reflect.Smart.GetFieldInt(0, Client_BaseY) +
    Reflect.Smart.GetFieldInt(Me.Reference, Actor_WorldY) div 128;
  Me.Free;
end;

function TReflectionTiles.GetPlane: Integer;
begin
  Result := Reflect.Smart.GetFieldInt(0, Client_Plane);
end;

	{*Credit Krazy_Meerkat*}
function TReflectionTiles.RotateCameraToTile(Tile: TTile): Boolean;
var
  Angle: Extended;
  Me: TPoint;
begin
  Me:= Reflect.Tiles.GetGlobalTile;
  Angle:= (Degrees(ArcTan2(Tile.Y - Me.Y, Tile.X - Me.X))) - 90;
  if (Angle < 0) then
      Angle:= 360 + angle;
  Angle:= 360 - angle;
  Result:= Reflect.Compass.Make(angle);
end;

function TReflectionTiles.DistanceFromTile(Tile: TTile): Integer;
var
  Loc: TPoint;
begin
  if (Tile.X > 100000) or (Tile.X < -100000) or (Tile.Y > 100000) or
    (Tile.Y < -100000) then
    Exit;
  Loc := Reflect.Tiles.GetGlobalTile;
  Result :=  Distance(Tile.X, Tile.Y, Loc.X, Loc.Y);
end;

function TReflectionTiles.NearTile(Tile: TTile; Dist: Integer): Boolean;
begin
  Result := Reflect.Tiles.DistanceFromTile(Tile) < Dist;
end;

function TReflectionTiles.InPolygon(const Poly: TPointArray): Boolean;
var
  P: TPoint;
  I, J: Integer;
begin
  Result := False;
  P := Reflect.Tiles.GetGlobalTile;
  J := High(Poly);
  for I := Low(Poly) to High(Poly) do
  begin
    if ((((Poly[I].Y <= P.Y) and (P.Y < Poly[J].Y)) or ((Poly[J].Y <= P.Y) and
     (P.Y < Poly[I].Y)) ) and (P.X < ((Poly[J].X - Poly[I].X) *
     (P.Y - Poly[I].Y) / (Poly[J].Y - Poly[I].Y) + Poly[I].X))) then
     Result := not Result;
    J := I;
  end;
end;

	{*Credit BenLand100, modified by Krazy_Meerkat*}
function TReflectionTiles.TileToMM(Tile: TTile): TPoint;
var
  Angle, X, Y: extended;
  Temp: TTile;
begin
  Angle := -Reflect.Compass.AngleRad;
  Temp := Reflect.Tiles.GetGlobalTile;
  X := (Tile.X - Temp.X) * 4 - 2;
  Y := (Temp.Y - Tile.Y) * 4 - 2;
  Result.X := Round((X * Cos(Angle)) + (Y * Sin(Angle))) + 643;
  Result.Y := Round((Y * Cos(angle)) - (X * Sin(Angle))) + 83;
end;

	{*Credit BenLand100, modified by Brandon*}
function TReflectionTiles.GetTileHeight(Tile: TTile): Integer;
var
  X, Y, CurPlane, GroundSetting: Integer;
begin
  X := Tile.X - Reflect.Smart.GetFieldInt(0, Client_BaseX);
  Y := Tile.Y - Reflect.Smart.GetFieldInt(0, Client_BaseY);
  if ((X < 0) or (X > 104) or (Y < 0) or (Y > 104)) then
  begin
    Result := 0;
    Exit;
  end;
  CurPlane := Reflect.Smart.GetFieldInt(0, Client_Plane);
  GroundSetting := Reflect.Smart.GetFieldArray3DByte(0,
    Client_TileSettings, 1, X, Y);
  if ((CurPlane < 3) and ((GroundSetting and 2) <> 0)) then
    CurPlane := CurPlane + 1;
  Result := Reflect.Smart.GetFieldArray3DInt(0, Client_TileHeights,
    CurPlane, X, Y)
  + Reflect.Smart.GetFieldArray3DInt(0, Client_TileHeights, CurPlane, X + 1, Y)
  + Reflect.Smart.GetFieldArray3DInt(0, Client_TileHeights, CurPlane, X, Y + 1)
  + Reflect.Smart.GetFieldArray3DInt(0, Client_TileHeights,
    CurPlane, X + 1, Y + 1);
  Result := - Result div 4;
end;

	{*Credit Dgby714*}
function Ashr(X: Integer; D: Byte): Integer;
var
  Bit: Integer;
  I: Integer;
begin
  Bit := (1 shl 31) and X;
  Result := X;
  for I := 1 to D do
    Result := (Result shr 1) or Bit;
end;

	{*Credit Frement*}
function TReflectionInternals.TileToMS(X, Y, Height: Extended): TPoint;
var
  Z, CurveX, CurveY, CurveCosX, CurveCosY, CurveSinX, CurveSinY: Extended;
  TempCalculation: Extended;
  CalculatedScreenPosX, CalculatedScreenPosY: Integer;
begin
  if (X < 128) or (Y < 128) or (X > 13056) or (Y > 13056) then
  begin
    Result := Point(-1, -1);
    Exit;
  end;
  Z := -Height;
  X := X - Reflect.Compass.CameraX;
  Y := Y - Reflect.Compass.CameraY;
  Z := Z - Reflect.Compass.CameraZ;
  CurveX := Reflect.Compass.GetYaw;
  CurveY := Reflect.Compass.GetPitch;
  CurveCosX := CURVECOS[Floor(CurveX)];
  CurveCosY := CURVECOS[Floor(CurveY)];
  CurveSinX := CURVESIN[Floor(CurveX)];
  CurveSinY := CURVESIN[Floor(CurveY)];
  TempCalculation := Ashr(Round((CurveCosX * X) + (Y * CurveSinX)), 16);
  Y := Ashr(Round((Y * CurveCosX) - (X * CurveSinX)), 16);
  X := Round(TempCalculation);
  TempCalculation := Ashr(Round((Z * CurveCosY) - (Y * CurveSinY)), 16);
  Y := Ashr(Round((Z * CurveSinY) - ((-CurveCosY) * Y)), 16);
  Z := Round(TempCalculation);
  if (Y < 50) then
  begin
    Result := Point(-1, -1);
    Exit;
  end else
  begin
    CalculatedScreenPosX := Round((((Floor(X) shl 9) / Y)));
    IncEx(CalculatedScreenPosX, 256);
    CalculatedScreenPosY := Round((((Floor(Z) shl 9) / Y)));
    IncEx(CalculatedScreenPosY, 167);
    Result := Point(CalculatedScreenPosX, CalculatedScreenPosY);
  end;
end;

function TReflectionTiles.TileToMS(Tile: TTile; XOffset: Integer = 0; YOffset:
  Integer = 0; ZOffset: Integer = 0): TPoint;
var
  X, Y, Z: Integer;
begin
  X := Round((Tile.X - Reflect.Smart.GetFieldInt(
    0, Client_BaseX) + 0.5) * 128) + XOffset;
  Y := Round((Tile.Y - Reflect.Smart.GetFieldInt(
    0, Client_BaseY) + 0.5) * 128) + YOffset;
  Z := Reflect.Tiles.GetTileHeight(Tile) + ZOffset;
  Result := Reflect.Internals.TileToMS(X, Y, Z);
end;

{* Credit Fitta *}
function TReflectionTiles.TileOnMS(Tile: TPoint): Boolean;
begin
  Result := PointInBox(Reflect.Tiles.TileToMS(Tile), IntToBox(MSX1, MSY1, MSX2, MSY2));
end;

{* Credit flamingo1 *}
function TReflectionTiles.TileRandom(Tile: TPoint; aFrom, aTo: Integer): TPoint;
begin
  Tile.X := Tile.X + RandomRange(aFrom, aTo);
  Tile.Y := Tile.Y + RandomRange(aFrom, aTo);

  Result := Tile;
end;

function TReflectionTiles.TileRandom(Tile: TPoint): TPoint; overload;
begin
  Result := TReflectionTiles.TileRandom(Tile, -1, 1);
end;
